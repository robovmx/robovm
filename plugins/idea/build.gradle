plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.4.20"
}

ext {
    roboVMVersion = '10.1.1-SNAPSHOT'
}

group 'com.robovmx'
version "${roboVMVersion}"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations {
    robovm_dist
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile ("com.robovmx:robovm-dist-compiler:${roboVMVersion}") {
        exclude group: 'com.robovmx', module: 'robovm-rt'
        exclude group: 'com.robovmx', module: 'robovm-objc'
        exclude group: 'com.robovmx', module: 'robovm-cocoatouch'
        exclude group: 'com.robovmx', module: 'robovm-cacerts-full'
    }
    compile ("com.robovmx:robovm-ibxcode:${roboVMVersion}") {
        exclude group: 'com.robovmx', module: 'robovm-compiler'
    }
    compile ("com.robovmx:robovm-templater:${roboVMVersion}") {
        exclude group: 'com.robovmx', module: 'robovm-templates-ios-single-view-no-ib'
        exclude group: 'com.robovmx', module: 'robovm-templates-ios-framework'
        exclude group: 'com.robovmx', module: 'robovm-templates-console'
    }
    robovm_dist "com.robovmx:robovm-dist:${roboVMVersion}:nocompiler@tar.gz"
}

intellij {
    version '2020.1'   // we are compiles against this API version
    plugins = ['java', 'maven', 'gradle']
    updateSinceUntilBuild = false
}

publishPlugin {
    distributionFile "org.robovm.idea-${roboVMVersion}-plugin-dist.jar"
}

task copyRoboVmDist(type: Copy) {
    from configurations.robovm_dist
    into "src/main/resources/"
    rename "robovm-dist-${roboVMVersion}-nocompiler.tar.gz", "robovm-dist"
}


sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava {
    dependsOn copyRoboVmDist
}
